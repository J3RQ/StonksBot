plugins {
    id "io.freefair.lombok" version "6.2.0"
    id "org.owasp.dependencycheck" version "6.3.2"
    id "com.github.spotbugs" version "4.7.1"
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id "com.github.ben-manes.versions" version "0.39.0"
    id 'se.patrikerdes.use-latest-versions' version '0.2.17'
    id "java"
}

repositories {
    mavenCentral()
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
}

test {
    useJUnitPlatform()
}

sourceSets {

    integrationTest {

        java.srcDir 'src/integration_test/java'
        resources.srcDir 'src/integration_test/resources'

        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}


configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    environment "oath1",  System.getenv('OATH1')
    environment "oath2",  System.getenv('OATH2')
}

integrationTest {
    useJUnitPlatform()
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


spotbugs {
}

spotbugsTest {
    enabled = false
}

dependencies {
    implementation 'org.springframework:spring-core:5.3.10'
    implementation 'org.springframework:spring-beans:5.3.10'
    implementation 'org.springframework:spring-context:5.3.10'
    implementation 'org.yaml:snakeyaml:1.29'
    implementation 'org.ta4j:ta4j-core:0.14'
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'org.json:json:20210307'
    implementation ('net.dv8tion:JDA:4.3.0_331') {
        exclude module: 'opus-java'
    }
    implementation 'org.slf4j:slf4j-api:2.0.0-alpha5'
    implementation 'org.apache.logging.log4j:log4j-slf4j18-impl:2.14.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.14.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.12.4'
}

group = 'com.etsubu.stonksbot'
version = '1.2-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

jar {
    manifest {
        attributes "Version" : project.version
        attributes "Main-Class": "com.etsubu.stonksbot.Main"
    }
}